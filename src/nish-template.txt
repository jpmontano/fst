Multichar_Symbols +NA +NI +NAPl +NIPl +NASg +NISg 1P+ 2P+ 3P+ Sg+ Pl+ Poss+ Ex+ +1P +2P +3P +Ex +Sg +Pl +Pej +Dim +Obv +Loc +Con +Poss +PossThm

LEXICON Root
	Nouns ;

LEXICON Nouns
	RegularNouns ;
	! InherentlyPlural ;  ! V p.182
	! MassNouns ; ...     ! V p.182

LEXICON RegularNouns
	PossPrefixes ;

LEXICON PossPrefixes
1P+Sg+Poss+:ni@		AllNounStems ;
2P+Sg+Poss+:gi@		AllNounStems ;
3P+Sg+Poss+:w@		AllNounStems ;
1P+Pl+Poss+:ni@		AllNounStems ;
1P+Ex+Poss+:gi@		AllNounStems ;
2P+Pl+Poss+:gi@		AllNounStems ;
3P+Pl+Poss+:w@		AllNounStems ;
					IndependentNouns ;  ! no poss. prefix => independent only

LEXICON AllNounStems
	IndependentNouns ;
	DependentNouns ;

LEXICON IndependentNouns  ! actual ind. nouns from lexical database go here...

LEXICON DependentNouns ! actual dep. nouns from lexical database go here

LEXICON NA_I
+NA:0	DiminutiveOrContemptive ;

LEXICON NA_Dim
+NA:0	PossTheme ;

LEXICON NA_Con
+NA:0	PossTheme ;

LEXICON NA_II
+NA:0	DiminutiveOrContemptive ;

LEXICON NA_III
+NA:^w	DiminutiveOrContemptive ;

! capital W here is a hack to distinguish from IVb;
! see spelling rewrite rules in nish.fst
LEXICON NA_IVa
+NA:^W	DiminutiveOrContemptive ;

LEXICON NA_IVb
+NA:^w	DiminutiveOrContemptive ;

LEXICON NA_V
+NA:^y	DiminutiveOrContemptive ;

LEXICON NA_VI
+NA:^A	DiminutiveOrContemptive ;

LEXICON NA_Dep
+NA:0	DiminutiveOrContemptive ;

LEXICON NI_I
+NI:0	DiminutiveOrContemptive ;

LEXICON NI_Dim
+NI:0	PossTheme ;

LEXICON NI_Con
+NI:0	PossTheme ;

LEXICON NI_II
+NI:0	DiminutiveOrContemptive ;

LEXICON NI_III
+NI:^w	DiminutiveOrContemptive ;

! capital W here is a hack to distinguish from IVb;
! see spelling rewrite rules in nish.fst
LEXICON NI_IVa
+NI:^W	DiminutiveOrContemptive ;

! Note no NI_IVb

LEXICON NI_V
+NI:^y	DiminutiveOrContemptive ;

LEXICON NI_VI
+NI:^A	DiminutiveOrContemptive ;

LEXICON NI_Dep
+NI:0	DiminutiveOrContemptive ;

LEXICON DiminutiveOrContemptive
+Dim:^ens	PossTheme ;
+Con:^eny	PossTheme ;
			PossTheme ;

LEXICON PossTheme
+PossThm:^im	Possessor ;
			Possessor ;  ! Possessive theme "im" is always optional V p.200

LEXICON Possessor  ! see table on V p.196-197
+1P+Sg+Poss+NASg:0			# ;
+2P+Sg+Poss+NASg:0			# ;
+3P+Sg+Poss+NASg:^an			# ;
+1P+Pl+Poss+NASg:^inaan		# ;
+1P+Ex+Poss+NASg:^inaan		# ;
+2P+Pl+Poss+NASg:^iwaa		# ;
+3P+Pl+Poss+NASg:^iwaan		# ;
+1P+Sg+Poss+NAPl:^ag			# ;
+2P+Sg+Poss+NAPl:^ag			# ;
+3P+Sg+Poss+NAPl:^an			# ;
+1P+Pl+Poss+NAPl:^inaanig		# ;
+1P+Ex+Poss+NAPl:^inaanig	# ;
+2P+Pl+Poss+NAPl:^iwaag		# ;
+3P+Pl+Poss+NAPl:^iwaan		# ;
+1P+Sg+Poss+Obv:^an			# ;
+2P+Sg+Poss+Obv:^an			# ;
+3P+Sg+Poss+Obv:^an			# ;
! +3P+Obv+Poss+Obv:^ini			# ;
+1P+Pl+Poss+Obv:^inaanin		# ;
+1P+Ex+Poss+Obv:^inaanin	# ;
+2P+Pl+Poss+Obv:^iwaan		# ;
+3P+Pl+Poss+Obv:^iwaan		# ;
+1P+Sg+Poss+NISg:0			# ;
+2P+Sg+Poss+NISg:0			# ;
+3P+Sg+Poss+NISg:0			# ;
+1P+Pl+Poss+NISg:^inaan		# ;
+1P+Ex+Poss+NISg:^inaan		# ;
+2P+Pl+Poss+NISg:^iwaa		# ;
+3P+Pl+Poss+NISg:^iwaa		# ;		! note typo on V p.197
+1P+Sg+Poss+NIPl:^an			# ;
+2P+Sg+Poss+NIPl:^an			# ;
+3P+Sg+Poss+NIPl:^an			# ;
+1P+Pl+Poss+NIPl:^inaanin		# ;
+1P+Ex+Poss+NIPl:^inaanin	# ;
+2P+Pl+Poss+NIPl:^iwaan		# ;
+3P+Pl+Poss+NIPl:^iwaan		# ;
+1P+Sg+Poss+Loc:^ing			# ;
+2P+Sg+Poss+Loc:^ing			# ;
+3P+Sg+Poss+Loc:^ing			# ;
+1P+Pl+Poss+Loc:^inaaning		# ;
+1P+Ex+Poss+Loc:^inaaning	# ;
+2P+Pl+Poss+Loc:^iwaang		# ;
+3P+Pl+Poss+Loc:^iwaang		# ;
							Pejorative ;

LEXICON Pejorative
+Pej:^ish	Number ;   ! V p.192 "underlying form of the pejorative is /-ish/"
			Number ;

LEXICON Number
+NAPl:^ag		# ;
+NIPl:^an		# ;
+Obv:^an		# ;	! NB only animate nouns have obviative (V p.183) 
+Loc:^ing		# ; ! V p.195 "basic form of the locative suffix is /-ing/"
+NASg:0			# ;
+NISg:0			# ;
